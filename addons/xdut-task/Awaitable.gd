## å°†æ¥æ±ºã¾ã‚‹å€¤ã‚’æŠ½è±¡åŒ–ã™ã‚‹ãŸã‚ã®å…±é€šã®ã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ã‚¤ã‚¹ã‚¯ãƒ©ã‚¹ã§ã™ã€‚
@abstract
class_name Awaitable

#-------------------------------------------------------------------------------
#	CONSTANTS
#-------------------------------------------------------------------------------

enum {
	## çµæžœã‚’å¾…æ©Ÿã—ã¦ã„ã¾ã™ã€‚
	STATE_PENDING,

	## çµæžœã‚’å¾…æ©Ÿã—ã¦ãŠã‚Šã€[method wait] ã«ã‚ˆã‚Šãƒ–ãƒ­ãƒƒã‚¯ã•ã‚Œã¦ã„ã‚‹å‘¼ã³å‡ºã—ãŒä¸€ã¤ä»¥ä¸Šã‚ã‚Šã¾ã™ã€‚
	STATE_PENDING_WITH_WAITERS,

	## å®Œäº†ã—ã¾ã—ãŸã€‚[br]
	## [br]
	## ðŸ’¡ ã“ã‚Œä»¥ä¸ŠçŠ¶æ…‹ã¯å¤‰åŒ–ã—ã¾ã›ã‚“ã€‚
	STATE_COMPLETED,

	## ã‚­ãƒ£ãƒ³ã‚»ãƒ«ã•ã‚Œã¾ã—ãŸã€‚[br]
	## [br]
	## ðŸ’¡ ã“ã‚Œä»¥ä¸ŠçŠ¶æ…‹ã¯å¤‰åŒ–ã—ã¾ã›ã‚“ã€‚
	STATE_CANCELED,
}

#-------------------------------------------------------------------------------
#	PROPERTIES
#-------------------------------------------------------------------------------

## ã“ã® [Awaitable] ãŒå®Œäº†ã—ã¦ã„ã‚‹å ´åˆã¯ [code]true[/code]ã€[br]
## ãã‚Œä»¥å¤–ã®å ´åˆã¯ [code]false[/code] ã‚’è¿”ã—ã¾ã™ã€‚[br]
## [br]
## ðŸ’¡ ã“ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã®è¿”ã™å€¤ã¯ã€[method get_state][code] == [/code][constant STATE_COMPLETED] ã¨ç­‰ä¾¡ã§ã™ã€‚
var is_completed: bool:
	get:
		return get_state() == STATE_COMPLETED

## ã“ã® [Awaitable] ãŒã‚­ãƒ£ãƒ³ã‚»ãƒ«ã•ã‚Œã¦ã„ã‚‹å ´åˆã¯ [code]true[/code]ã€[br]
## ãã‚Œä»¥å¤–ã®å ´åˆã¯ [code]false[/code] ã‚’è¿”ã—ã¾ã™ã€‚[br]
## [br]
## ðŸ’¡ ã“ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã®è¿”ã™å€¤ã¯ã€[method get_state][code] == [/code][constant STATE_CANCELED] ã¨ç­‰ä¾¡ã§ã™ã€‚
var is_canceled: bool:
	get:
		return get_state() == STATE_CANCELED

## ã“ã® [Awaitable] ãŒå®Œäº†ã‚‚ã‚­ãƒ£ãƒ³ã‚»ãƒ«ã‚‚ã•ã‚Œã¦ãŠã‚‰ãšçµæžœã‚’å¾…æ©Ÿã—ã¦ã„ã‚‹å ´åˆã¯ [code]true[/code]ã€
## ãã‚Œä»¥å¤–ã®å ´åˆã¯ [code]false[/code] ã‚’è¿”ã—ã¾ã™ã€‚[br]
## [br]
## ðŸ’¡ ã“ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã®è¿”ã™å€¤ã¯ã€[method get_state][code] in [[/code][constant STATE_PENDING][code], [/code][constant STATE_PENDING_WITH_WAITERS][code]][/code] ã¨ç­‰ä¾¡ã§ã™ã€‚
var is_pending: bool:
	get:
		var state := get_state()
		return \
			state == STATE_PENDING or \
			state == STATE_PENDING_WITH_WAITERS

#-------------------------------------------------------------------------------
#	METHODS
#-------------------------------------------------------------------------------

static func internal_get_task_canonical() -> Node:
	if not is_instance_valid(_task_canonical):
		_task_canonical = Engine \
			.get_main_loop() \
			.root \
			.get_node("/root/XDUT_TaskCanonical")
	assert(is_instance_valid(_task_canonical), "XDUT Task is not activated.")
	return _task_canonical

## ã“ã® [Awaitable] ã®çŠ¶æ…‹ã‚’å–å¾—ã—ã¾ã™ã€‚
@abstract
func get_state() -> int

## ã“ã® [Awaitable] ã®çµæžœãŒæ±ºã¾ã‚‹ã¾ã§å¾…æ©Ÿã—ã¾ã™ã€‚[br]
## [br]
## ðŸ’¡ ã‚­ãƒ£ãƒ³ã‚»ãƒ«ã•ã‚Œã¦ã„ã‚‹å ´åˆã¯ [code]null[/code] ã‚’è¿”ã—ã¾ã™ã€‚
@abstract
func wait(cancel: Cancel = null) -> Variant

#-------------------------------------------------------------------------------

static var _task_canonical: Node

func _to_string() -> String:
	var prefix: String
	match get_state():
		STATE_PENDING:
			prefix = internal_get_task_canonical() \
				.translate(&"TASK_STATE_PENDING")
		STATE_PENDING_WITH_WAITERS:
			prefix = internal_get_task_canonical() \
				.translate(&"TASK_STATE_PENDING_WITH_WAITERS")
		STATE_CANCELED:
			prefix = internal_get_task_canonical() \
				.translate(&"TASK_STATE_CANCELED")
		STATE_COMPLETED:
			prefix = internal_get_task_canonical() \
				.translate(&"TASK_STATE_COMPLETED")
		_:
			assert(false)
	return &"%s<Awaitable#%d>" % [prefix, get_instance_id()]
